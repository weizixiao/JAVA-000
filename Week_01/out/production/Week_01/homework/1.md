### 源文件
```
public class Hello {
    public static void main(String[] args) {
        byte num1 = 10;
        byte num2 = 5;
        int numAdd = num1 + num2;
        int numSub = num1 - num2;
        int numMult = num1 * num2;
        int numDivide = num2 / num1;
        if (numAdd >= 15) {
            int sum = 0;
            for (int i = 0; i < 10; i++) {
                sum += i;
            }
        } else {
            System.out.println(numAdd);
        }
    }
}
```
使用**javac Hello.java**编译源文件
### 字节码文件
用**javap -c -verbose Hello.class**反编译class文件

字节码指令列表：
https://docs.oracle.com/javase/specs/jvms/se8/html/
```
Classfile /Users/weizi/Desktop/javaStudy/geekTime/10-15/code/src/Hello.class
  // 编译时间
  Last modified Oct 16, 2020; size 559 bytes
  // MD5校验
  MD5 checksum e62ca08f898b67b3b9028eb4ba7d5dfe
  // 编译源文件
  Compiled from "Hello.java"
public class Hello
// java版本号
  minor version: 0
  major version: 52
// class修饰符
  flags: ACC_PUBLIC, ACC_SUPER
// 常量池
Constant pool:
   #1 = Methodref          #5.#16         // java/lang/Object."<init>":()V
   #2 = Fieldref           #17.#18        // java/lang/System.out:Ljava/io/PrintStream;
   #3 = Methodref          #19.#20        // java/io/PrintStream.println:(I)V
   #4 = Class              #21            // Hello
   #5 = Class              #22            // java/lang/Object
   #6 = Utf8               <init>
   #7 = Utf8               ()V
   #8 = Utf8               Code
   #9 = Utf8               LineNumberTable
  #10 = Utf8               main
  #11 = Utf8               ([Ljava/lang/String;)V
  #12 = Utf8               StackMapTable
  #13 = Class              #23            // "[Ljava/lang/String;"
  #14 = Utf8               SourceFile
  #15 = Utf8               Hello.java
  #16 = NameAndType        #6:#7          // "<init>":()V
  #17 = Class              #24            // java/lang/System
  #18 = NameAndType        #25:#26        // out:Ljava/io/PrintStream;
  #19 = Class              #27            // java/io/PrintStream
  #20 = NameAndType        #28:#29        // println:(I)V
  #21 = Utf8               Hello
  #22 = Utf8               java/lang/Object
  #23 = Utf8               [Ljava/lang/String;
  #24 = Utf8               java/lang/System
  #25 = Utf8               out
  #26 = Utf8               Ljava/io/PrintStream;
  #27 = Utf8               java/io/PrintStream
  #28 = Utf8               println
  #29 = Utf8               (I)V
{
  // 默认构造方法
  public Hello();
    // 无返回值方法
    descriptor: ()V
    // 访问修饰符
    flags: ACC_PUBLIC
    Code:
      // 压栈暂存数据需要长度
      // 变量数
      // 这边会发现，args_size是1，是默认会有个this传入
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 1: 0
// main方法执行
  public static void main(java.lang.String[]);
    // [表示数组，L表示对象，java/lang/String 字符串对象数组
    // V表示无返回值
    descriptor: ([Ljava/lang/String;)V
    // ACC_PUBLIC表示public，ACC_STATIC表示static
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      // 下面有申请八个局部变量，加上参数，九个
      // stack，一般运算完一次，求值栈就清了，所以最深栈一般都不大
      stack=2, locals=9, args_size=1
         // 执行指令，在方法执行的时候，会有一个计算器，计数器会一行行往下走
         0: bipush        10  // 常量10压入求值栈
         2: istore_1  // 从求值栈中取出，保存到局部变量表第1个位置
         3: iconst_5  // 常量5压入求值栈
         4: istore_2  // 从求值栈中取出，保存到局部变量表第2个位置
         5: iload_1   // 取局部变量的第一个值，压入栈顶
         6: iload_2   // 取局部变量的第二个值，压入栈顶
         7: iadd      // 加法求值
         8: istore_3  // 加法得到的值，保存到局部变量表第3个位置
         9: iload_1   // 取局部变量的第一个值，压入栈顶
        10: iload_2   // 取局部变量的第二个值，压入栈顶
        11: isub      // 减法运算
        12: istore        4 // 运算的值存入到局部表量表第四个位置
        14: iload_1   // 取局部变量的第一个值，压入栈顶
        15: iload_2   // 取局部变量的第二个值，压入栈顶
        16: imul      // 乘法运算
        17: istore        5 // 运算的值存入到局部表量表第五个位置
        19: iload_2   // 取局部变量的第一个值，压入栈顶
        20: iload_1   // 取局部变量的第二个值，压入栈顶
        21: idiv      // 除法运算
        22: istore        6 // 运算的值存入到局部表量表第六个位置
        24: iload_3   // 取局部变量的第三个值，压入栈顶（加法运算结果）
        25: bipush        15 // 常量10压入求值栈
        27: if_icmplt     59 // 条件判断，成功继续走，不成功跳到59标记位
        30: iconst_0  // 常量0压入求值栈
        31: istore        7 // 运算的值存入到局部表量表第七个位置
        33: iconst_0  // 常量0压入求值栈
        34: istore        8 // 运算的值存入到局部表量表第八个位置
        36: iload         8 // 取局部变量的第八个值，压入栈顶
        38: bipush        10 // 常量10压入求值栈
        40: if_icmpge     56 // for循环的条件判断
        43: iload         7  // 取局部变量的第七个值，压入栈顶
        45: iload         8  // 取局部变量的第八个值，压入栈顶
        47: iadd      // 加法运算，求sum
        48: istore        7  // 存入局部变量第七个的值
        50: iinc          8, 1  // 取出第八个局部变量，自增加1
        53: goto          36 // 跳转到36，循环运算
        56: goto          66 // 跳转到66
        // 引用类
        59: getstatic     #2                  // Field java/lang/System.out:Ljava/io/PrintStream;
        62: iload_3  // 加载第三个常量，压入栈
        // 执行方法
        63: invokevirtual #3                  // Method java/io/PrintStream.println:(I)V
        66: return
      // 源文件函数和上面指令位对应表
      LineNumberTable:
        line 3: 0
        line 4: 3
        line 5: 5
        line 6: 9
        line 7: 14
        line 8: 19
        line 9: 24
        line 10: 30
        line 11: 33
        line 12: 43
        line 11: 50
        line 14: 56
        line 15: 59
        line 17: 66
      StackMapTable: number_of_entries = 4
        frame_type = 255 /* full_frame */
          offset_delta = 36
          locals = [ class "[Ljava/lang/String;", int, int, int, int, int, int, int, int ]
          stack = []
        frame_type = 249 /* chop */
          offset_delta = 19
        frame_type = 2 /* same */
        frame_type = 6 /* same */
}
SourceFile: "Hello.java"
```